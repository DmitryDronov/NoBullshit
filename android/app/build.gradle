apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "kotlin-allopen"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.nobullshit.nobullshit"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1 // Will be overridden by Bitrise
        versionName "1.0-beta"
        testInstrumentationRunner "io.nobullshit.nobullshit.TIRunner"
    }
    buildTypes {
        debug {  }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }
}

// allows mocking for classes w/o directly opening them for release builds
allOpen {
    annotation("io.nobullshit.nobullshit.testing.OpenForTesting")
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // ANDROID
    implementation "androidx.appcompat:appcompat:$android_appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$android_constraint_layout_version"
    implementation "com.google.android.material:material:$android_material_version"
    implementation "androidx.paging:paging-runtime-ktx:$android_pagging_version"
    // FIREBASE
    implementation "com.google.firebase:firebase-core:$firebase_version"
    implementation "com.firebaseui:firebase-ui-firestore:$firestore_ui_version"
    // GLIDE
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    // DAGGER 2
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    compileOnly 'javax.annotation:jsr250-api:1.0'
    // CUSTOM TABS
    implementation "androidx.browser:browser:$custom_tab_version"
    // UNIT TEST
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    // INSTRUMENTED TEST
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'
    androidTestImplementation "io.mockk:mockk-android:1.8.13"
    kaptAndroidTest 'com.google.dagger:dagger:2.17'
}
apply plugin: 'com.google.gms.google-services'
